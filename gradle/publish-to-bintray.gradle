apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '0.1.0'
group 'cn.woodyhi'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/$project.name-release.aar")
            groupId this.group
            artifactId 'floating-view'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
						dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : System.getenv('BINTRAY_API_KEY')
    publications = ['Production']

//    dryRun = true // [默认:false] 如果true则不会上传
    publish = true //[Default: false] 上传后是否发布
    override = true //[Default: false] Whether to override version artifacts already published

    pkg {
        repo = 'maven' // 仓库名
        name = 'floating-view' // 项目名称
        description = "simple floating-view on android screen"
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/woodyhi/floating-view.git'
        version {
            name = this.version
            desc = "${project.name}-${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}